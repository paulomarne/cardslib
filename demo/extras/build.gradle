apply plugin: 'com.android.application'

repositories {
    mavenCentral()
    //used only for twowayview
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        versionName project.VERSION_NAME
        versionCode Integer.parseInt(project.VERSION_CODE)
    }

    signingConfigs {
        release
    }

    buildTypes {
        if (isReleaseBuild()) {
            release {
                signingConfig signingConfigs.release
            }
        }

        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
        }
    }

    // This is important, it will run lint checks but won't abort build
    lintOptions {
        abortOnError false
    }
}

dependencies {
    // Cards Library
    implementation project(':library-core')
    implementation project(':library-cards')
    implementation project(':extra:extra-twoway')
    implementation project(':extra:extra-staggeredgrid')
    implementation project(':extra:extra-dragdrop')

    // Support Libraries
    implementation 'com.android.support:support-v4:28.0.0'
    implementation 'com.android.support:appcompat-v7:28.0.0'

    //OkHttp
    //temporary patch for android-L for Okio packaging problem (see https://gist.github.com/JakeWharton/017738659d4f38adedc2)
    //compile fileTree(dir: 'libs', include: 'repackaged-*.jar')
    implementation 'com.squareup.okhttp:okhttp:2.0.0'
    implementation 'com.squareup.okhttp:okhttp-urlconnection:2.0.0'
    //Picasso
    implementation 'com.squareup.picasso:picasso:2.71828'

    //Ion
    //compile 'com.koushikdutta.ion:ion:1.3.1'

    //universal-image-loader
    implementation 'com.nostra13.universalimageloader:universal-image-loader:1.9.1'

    //Actionbar pull to refresh
    implementation 'com.github.chrisbanes.actionbarpulltorefresh:library:0.9.9'

    //lListViewAnimations
    implementation 'com.nhaarman.listviewanimations:lib-core:3.1.0'
    implementation 'com.nhaarman.listviewanimations:lib-manipulation:3.1.0'

    //Crouton
    implementation('de.keyboardsurfer.android.widget:crouton:1.8.5@aar') {
        // exclusion is not neccessary, but generally a good idea.
        exclude group: 'com.google.android', module: 'support-v4'
    }

    //StickyListHeaders
    implementation ('se.emilsjolander:stickylistheaders:2.5.0')

    //Weather Lib
    implementation 'com.survivingwithandroid:weatherlib:1.3.2'

    // Fading ActionBar
    //compile 'com.github.manuelpeinado.fadingactionbar:fadingactionbar:3.0.0'

    //Google Progress Bar
    //compile 'com.jpardogo.googleprogressbar:library:1.1.0'

    implementation 'com.github.castorflex.smoothprogressbar:library-circular:1.0.1'

    //FAB
    implementation 'com.melnykov:floatingactionbutton:1.0.6'
}


def Properties props = new Properties()
def propFile = file('../../signing.properties')
if (propFile.canRead()){
    props.load(new FileInputStream(propFile))

    if (props!=null && props.containsKey('STORE_FILE') && props.containsKey('STORE_PASSWORD') &&
            props.containsKey('KEY_ALIAS') && props.containsKey('KEY_PASSWORD')) {

        println 'RELEASE BUILD SIGNING'

        android.signingConfigs.release.storeFile = file(props['STORE_FILE'])
        android.signingConfigs.release.storePassword = props['STORE_PASSWORD']
        android.signingConfigs.release.keyAlias = props['KEY_ALIAS']
        android.signingConfigs.release.keyPassword = props['KEY_PASSWORD']
    } else {
        println 'RELEASE BUILD NOT FOUND SIGNING PROPERTIES'

        android.buildTypes.release.signingConfig = null
    }
}else {
    println 'RELEASE BUILD NOT FOUND SIGNING FILE'
    android.buildTypes.release.signingConfig = null
}